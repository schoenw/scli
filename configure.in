dnl Process this file with autoconf to produce a configure script.

AC_INIT(stop/stop.c)
dnl Every other copy of the package version number gets its value from here
AM_INIT_AUTOMAKE(stools, 0.1.8)

dnl create a config.h file (Automake will add -DHAVE_CONFIG_H)
AM_CONFIG_HEADER(config.h)

AC_SUBST(VERSION)

ISODATE=`date +%Y-%m-%d`
AC_SUBST(ISODATE)

AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_CC
AC_PROG_RANLIB
AC_CHECK_PROG(SMIDUMP, smidump, smidump)
AC_SUBST(SMIDUMP)

dnl Checks for libraries.

AM_PATH_GLIB(1.2.0)
AC_CHECK_LIB(ncurses, initscr, , 
  AC_CHECK_LIB(curses, initscr, ,
    AC_MSG_ERROR([stools require curses functions; install curses first])
  )
)
AC_CHECK_LIB(readline, readline, ,
  AC_MSG_ERROR([stools require readline functions; install libreadline first])
)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h sys/ioctl.h sys/time.h ncurses.h)

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.

AC_CHECK_FUNC(gethostbyname)
if test $ac_cv_func_gethostbyname = no; then
  AC_CHECK_LIB(nsl, gethostbyname)
fi

AC_CHECK_FUNC(connect)
if test $ac_cv_func_connect = no; then
  AC_CHECK_LIB(socket, connect)
fi

AC_CHECK_FUNCS(resizeterm)

dnl Check for configure options

AC_ARG_ENABLE(dmalloc,
[  --enable-dmalloc        enable dmalloc debugging (www.dmalloc.com)],
  AC_CHECK_HEADERS(dmalloc.h)
  AC_CHECK_LIB(dmalloc, malloc) 
)

dnl Create all the Makefiles we need and we are done.

AC_OUTPUT(Makefile snmp/Makefile stub/Makefile stop/Makefile scli/Makefile doc/Makefile)
